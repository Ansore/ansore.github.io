---
title: 64位操作系统-实现系统调用
tags:
  - 64位系统实现
  - 操作系统
categories:
  - 操作系统
cover: 'https://img.ansore.top/2022/09/11/f01cf15d850f0ce8f2c97c93a206411c8d3bf927.png'
date: 2022-09-05 10:26:06
---

# SYSENTER指令

`SYSENTER`指令可以实现应用层到内核层的跳转。在执行`SYSENTER`之前，处理器必须为其提供0特权级的衔接程序以及0特权级的栈空间，这些数据将会保存在`MSR`寄存器组和通用寄存器中。

- `IA32_SYSENTER_CS`，位于`MSR`寄存器组的地址`174h`处，这个`MSR`寄存器的低16位装载的是0特权级的代码段选择子，该值也用于所以0特权级的栈段选择子（`IA32_SYSENTER_CS[15:0]+8`），所以该值不能为NULL
- `IA32_SYSENTER_ESP`，位于`MSR`寄存器组的地址`175h`处，这个`MSR`寄存器里的值会被载入`RSP`寄存器中，该值必须是Canonical型地址。在保护模式下，只有寄存器的低32位会被载入`RSP寄存器`。

- `IA32_SYSENTER_EIP`，位于`MSR`寄存器组的地址`176h`处，这个`MSR`寄存器里的值会被载入`RIP`寄存器中，该值必须是Canonical型地址。在保护模式下，只有寄存器的低32位会被载入`RIP寄存器`。

在执行`SYSENTER`指令的过程中，处理器会根据`IA32_SYSENTER_CS`寄存器的值加载相应的段选择子到`CS`和`SS`寄存器。

`SYSENTER/SYSEXIT`指令与`CALL/RET`指令的不同之处在于，执行`SYSENTER`指令时，处理器不会保存用户代码的状态信息（`RIP`和`RSP`寄存器的值），而且两者均不支持内存参数方式。同时`SYSENTER/SYSEXIT`还必须遵循如下规则：

- `SYSENTER/SYSEXIT`指令使用的段描述符皆位于同一个段描述符内，并且各个段描述符是相邻的。只有这样才能使处理器根据`SYSENTER_CS_MSR`寄存器值索引到段选择子。
- 应用程序在执行`SYSENTER`指令进入内核层时，必须保存程序的运行环境，并在执行`SYSEXIT`指令返回应用层时恢复程序的运行环境。

# 实现系统调用

`system_call`模块实现：

```assembly
// ...
ENTRY(system_call)
  sti # SYSENTER汇编指令将会使RFLGAS.IF标志复位，在进入内核层后，必须手动使能中断（置位IF标志位）
  subq $0x38, %rsp
  cld
  
  push %rax
  movq %es, %rax
  pushq %rax
  movq %ds, %rax
  pushq %rax
  xorq %rax, %rax
  pushq %rbp
  pushq %rdi
  pushq %rsi
  pushq %rdx
  pushq %rcx
  pushq %rbx
  pushq %r8
  pushq %r9
  pushq %r10
  pushq %r11
  pushq %r12
  pushq %r13
  pushq %r14
  pushq %r15
  movq $0x10, %rdx
  movq %rdx, %ds
  movq %rdx, %es
  movq %rsp, %rdi

  callq system_call_function

ENTRY(ret_system_call)
  movq %rax, 0x80(%rsp)
  popq %r15
  popq %r14
  popq %r13
  popq %r12
  popq %r11
  popq %r10
  popq %r9
  popq %r8
  popq %rbx
  popq %rcx
  popq %rdx
  popq %rsi
  popq %rdi
  popq %rbp
  popq %rax
  movq %rax, %ds
  popq %rax
  movq %rax, %es
  popq %rax
  addq $0x38, %rsp
  .byte 0x48
  sysexit
```

`system_call`模块必须在`ret_system_call`之前，在执行完成之后返回，也可以用`JMP`指令跳转。

`system_call`模块是系统调用API的接口模块。当应用程序执行`SYSENTER`指令进入内核层时，便会通过`system_call`模块保存应用程序执行现场，随后使用`CALL`指令调用`system_call_function`函数。在调用时`system_call`模块会将当前栈指针作为参数传递给`system_call_function`函数（`movq %rsp, %rdi`），此时的栈指针作为参数传递给`system_call_function`函数。

`system_call_function`函数实现：

```c
unsigned long system_call_function(struct pt_regs *regs) {
  return system_call_table[regs->rax](regs);
}
```

参数`regs`记录着进程执行环境，其中成员变量`rax`保存着系统调用API的向量号，暂定定128个。

数组`system_call_table`用于保存每个系统调用的处理函数，目前尚未实现，都赋值为`no_system_call`：

```c
#define MAX_SYSTEM_CALL_NR 128

typedef unsigned long (*system_call_t)(struct pt_regs *regs);

unsigned long no_system_call(struct pt_regs *regs) {
  color_printk(RED, BLACK, "no_system_call is called, NR: %#04x\n", regs->rax);
  return -1;
}

system_call_t system_call_table[MAX_SYSTEM_CALL_NR] = {
  [0 ... MAX_SYSTEM_CALL_NR-1] = no_system_call
};
```

此外还需要为`SYSENTER`汇编指令内核层指针以及系统调用在内核层的入口地址（`system_call`的起始地址）。分别将两个值的写入`MSR`寄存器的`175h`和`176h`地址处：

```c
void task_init() {
  // ...
  wrmsr(0x174, KERNEL_CS);
  wrmsr(0x175, current->thread->rsp0);
  wrmsr(0x176, (unsigned long)system_call);
  // ...
}
```

执行系统调用API。

在编写调用程序时，`SYSEXIT`指令执行须要向`RCX`和`RDX`写入程序返回地址和栈顶地址。所以在执行`SYSENTER`指令前，将程序的返回地址和栈顶地址保存在这两个寄存器内：

```c
void user_level_function() {
  long ret = 0;
  __asm__ __volatile__("leaq sysexit_return_address(%%rip), %%rdx \n\t"
                       "movq %%rsp, %%rcx \n\t"
                       "sysenter \n\t"
                       "sysexit_return_address: \n\t"
                       : "=a"(ret)
                       : "0"(15)
                       : "memory");
  while (1)
    ;
}
```

通过汇编`leaq`指令取得标识符`sysexit_return_address`的有效地址，并将有效地址保存到`RDX`寄存器中。`RCX`寄存器保存着应用层当前的栈指针，`RAX`寄存器保存系统调用API的向量号。

运行结果：

![Screenshot_20220828_003054](https://img.ansore.top/2022/08/28/62648c8e01dbf948fe3e6c6144461fdc.png)
